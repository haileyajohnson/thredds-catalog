apply plugin: 'com.google.protobuf'
apply plugin: 'java'
apply plugin: 'jacoco'

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${depVersion.protobuf}"
  }
}

// Add generated sources to the main sourceSet, which keeps intellij happy.
// Now, we could use the idea plugin and let gradle generate intellij project files, but it seems as though gradle is
// considering deprecating their plugin and upstreaming it to JetBrains. Might not be a good bet to start relying on
// the idea plugin at this point.
// https://github.com/gradle/gradle/issues/1366#issuecomment-546928184
sourceSets.main.java.srcDirs += ['build/generated/source/proto/main/java']

jacocoTestReport {
  afterEvaluate {
    // Exclude proto generated sources and classes from the coverage reports
    sourceDirectories.setFrom(files(sourceDirectories.files.collect {
      fileTree(dir: it, excludes:['**/*Proto.java'])
    }))
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, excludes:['**/*Proto.class', '**/*Proto$*.class'])
    }))
  }
}
